apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'mtj-circle-plugin'


enum ApiEnvEnum {
    DEV(0, "澳雷权益"), ONLINE(1, "澳雷权益"), PRE_RELEASE(2, "澳雷权益")

    private final int value
    private final String name

    ApiEnvEnum(int value, String name) {
        this.value = value
        this.name = name
    }

    int getValue() {
        value
    }

    String getName() {
        name
    }
}


android {

    def envStr = project.hasProperty('API_MODE') ? API_MODE : "DEV"
    def appNameStr = (envStr as ApiEnvEnum).name

    compileSdkVersion 28
    defaultConfig {
        applicationId rootProject.ext.applicationId
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        flavorDimensions "versionCode"
        multiDexEnabled true
        buildConfigField("int", "API_MODE", "${(envStr as ApiEnvEnum).value}")
        manifestPlaceholders = [APP_NAME     : appNameStr, UMENG_APPKEY: '5d1afe890cafb29716000c51',
                                UMENG_CHANNEL: 'aurei', BAIDU_KEY: "79919f0bc4",CHANNEL:"quan_yi"]

        ndk {
            abiFilters("armeabi", "armeabi-v7a", "x86", "mips")
        }
    }
//    5d1afe890cafb29716000c51




    signingConfigs {




        debug {
            storeFile file('../keystores/quanyi.jks')
            storePassword 'asdfghjkl'
            keyAlias 'quanyi'
            keyPassword 'asdfghjkl'
        }
        release {
            storeFile file('../keystores/quanyi.jks')
            storePassword 'asdfghjkl'
            keyAlias 'quanyi'
            keyPassword 'asdfghjkl'
            v2SigningEnabled true
        }
    }


    productFlavors {
        quanyi {
            applicationId rootProject.ext.applicationId
            manifestPlaceholders = [APP_NAME     : appNameStr, UMENG_APPKEY: '5d1afe890cafb29716000c51',
                                    UMENG_CHANNEL: 'aurei', BAIDU_KEY: "79919f0bc4",CHANNEL:"quan_yi"]
        }
    }


    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFileAbsolutePath = output.outputFileName
            def isRelease = outputFileAbsolutePath.indexOf("release")
            outputFileName = isRelease >= 0 ? "app-release.apk" : "app-debug.apk"
        }

        variant.outputs.all { output ->
            def versionCodes = ["armeabi-v7a": 1,'armeabi': 2,"x86": 3,"mips": 4]
            def abi = output.getFilter(com.android.build.OutputFile.ABI)
            if (abi != null) {
                output.versionCodeOverride = versionCodes.get(abi) * 1048576 + defaultConfig.versionCode
            }
        }
    }


    buildTypes {

        def defaultProguardRule = 'proguard-rules.pro'
        def proguardBaseRule = '../proguard-rule/proguard-rules-base.pro'
        def proguardReacNativeRule = '../proguard-rule/proguard-rules-react-native-0.46.0.pro'
        def proguardRemoveLogRule = '../proguard-rule/proguard-rules-remove-logs.pro'
        def proguardThirdPartServiceRule = '../proguard-rule/proguard-rules-third-part-service.pro'
        def proguardUmengSharedRule = '../proguard-rule/proguard-rules-umeng-share.pro'

        debug {
            crunchPngs false //关闭crunchPng优化, 以加快构建
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.release
        }

        release {
            productFlavors.quanyi.signingConfig signingConfigs.release
            debuggable false
            minifyEnabled true
            zipAlignEnabled true

            proguardFiles getDefaultProguardFile("proguard-android.txt"), defaultProguardRule,
                    proguardBaseRule, proguardReacNativeRule,
                    proguardRemoveLogRule,
                    proguardThirdPartServiceRule,
                    proguardUmengSharedRule
        }
    }


    packagingOptions{
        doNotStrip "*/armeabi-v7a/libkeyboardGuard.so"
        doNotStrip "*/arm64-v8a/libkeyboardGuard.so"
        doNotStrip "*/x86/libkeyboardGuard.so"
        doNotStrip "*/x86_64/libkeyboardGuard.so"
        doNotStrip "*/armeabi/libkeyboardGuard.so"

    }


    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    splits {
        abi {
            enable true
            reset()
            include "armeabi", "armeabi-v7a", "x86", "mips"
            universalApk true
        }
    }
    MtjCirclePluginConfig {
        // 设置appkey，必须设置，否则插件使用无效
        appkey = '79919f0bc4'
        // 设置debug 开关，默认关闭，如果需要查看日志则打开开关（true），建议正式版关闭以避免影响性能
        debug = false
        // 默认启动此插件，如果开发者不需要可以禁用（false）
        enabled = true
    }

    sourceSets {

        main {

            jniLibs.srcDirs = ['libs']

        }

    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation(name: 'CTAccountSdk_HY_v3.6.1_armeabi', ext: 'aar')
    implementation(name: 'keyboardGuard', ext: 'aar')
    implementation(name: 'alipaySdk-15.6.3-20190605142540', ext: 'aar')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation project(path: ':common')
    implementation project(path: ':http-lib')
    implementation project(':share')
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.5@aar'
    implementation 'com.github.LuckSiege.PictureSelector:picture_library:2.2.3'
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.12'
    implementation files('libs/passguard.jar')
    implementation files('libs/BestpaySDK-V4.1.0.jar')
}

repositories {
    maven { url 'https://jitpack.io' }
    flatDir {
        dirs 'libs'
    }
}